/**
 * Copyright 2019 Hadi Lashkari Ghouchani

 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at

 * http://www.apache.org/licenses/LICENSE-2.0

 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

group = Versions.groupId
archivesBaseName = Versions.artifactId
version = Versions.libVersion

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'LiveEvent'
                packaging 'aar'
                // optionally artifactId can be defined here
                description 'This library holds a class to handle single live events in Android MVVM architectural pattern. This class is extended ' +
                        'form LiveData class, from `androidx.lifecycle:lifecycle-extensions` library, to propagate the data as an event, ' +
                        'which means it emits data just once.'
                url 'https://github.com/hadilq/LiveEvent'

                scm {
                    connection 'scm:git@github.com:hadilq/LiveEvent.git'
                    developerConnection 'scm:git@github.com:hadilq/LiveEvent.git'
                    url 'https://github.com/hadilq/LiveEvent'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'hadilq'
                        name 'Hadi Lashkari Ghouchani'
                        email 'hadilq.dev@gmail.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}